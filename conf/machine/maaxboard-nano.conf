#@TYPE: Machine
#@NAME: Avnet MaaxBoard-Nano
#@SOC: i.MX8M Nano
#@DESCRIPTION: Machine configuration for Avnet MaaxBoard-Nano 1G, DDR4, SDCard
#@MAINTAINER: Leevi Li <leevi.li@avnet.com>
#@REFERENCE: meta-freescale/conf/machine/imx8mn-ddr4-evk.conf

MACHINEOVERRIDES =. "mx8:mx8m:mx8mn:"
MACHINEOVERRIDES =. "maaxboardbase:maaxboardnano:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/armv8a/tune-cortexa53.inc

MACHINE_FEATURES += " wifi bluetooth bcm43455"

# Set DDR FIRMWARE, Refer to: 
# meta-imx/meta-bsp/recipes-bsp/imx-mkimage/imx-boot_1.0.bb
# meta-imx/meta-bsp/recipes-bsp/firmware-imx/firmware-imx-8_8.18.bb
DDR_FIRMWARE_NAME = " ddr4_imem_1d*.bin ddr4_dmem_1d*.bin ddr4_imem_2d*.bin ddr4_dmem_2d*.bin "

# Set linux DTB
KERNEL_DEVICETREE = "freescale/maaxboard-nano.dtb"
KERNEL_IMAGETYPE = "Image"

# Set u-boot DTB 
UBOOT_DTB_NAME = "maaxboard-nano.dtb"
LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = ""
SPL_BINARY = "spl/u-boot-spl.bin"
UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "maaxboard-nano_defconfig,sdcard"
SERIAL_CONSOLES = "115200;ttymxc1"

# Set ATF platform name
ATF_PLATFORM = "imx8mn"

# Set imx-mkimage boot target
IMXBOOT_TARGETS = "flash_ddr4_evk"
IMX_BOOT_SOC_TARGET = "iMX8MN"
IMX_BOOT_SEEK = "32"
OPTEE_BIN_EXT = "8mn"

SOC_DEFAULT_IMAGE_FSTYPES:append = " wic"

# Remove unused QT example program
#CORE_IMAGE_EXTRA_INSTALL:remove += " qtbase-ptest qtbase-examples"

# Boot partition size [in KiB] for kernel and other data
# More refer to: meta-freescale/classes/image_types_fsl.bbclass
BOOT_SPACE = "65536"

# Add uEnv.txt file to FAT partition
WKS_FILE_DEPENDS:append = " uenv"
IMAGE_BOOT_FILES += "uEnv.txt"
IMAGE_BOOT_FILES += "readme.txt"
IMAGE_BOOT_FILES += "overlays/*;overlays/"

# /etc/hostname
hostname:pn-base-files = "maaxboardnano"

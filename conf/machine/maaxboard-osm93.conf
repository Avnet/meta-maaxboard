#@TYPE: Machine
#@NAME: Avnet MaaxBoard OSM93
#@SOC: i.MX93
#@DESCRIPTION: Machine configuration for Avnet MaaxBoard OSM93 with LPDDR4
#@MAINTAINER: Wenxue Guo <wenxue.guo@avnet.com>
#@REFERENCE: meta-freescale/conf/machine/imx93-11x11-lpddr4x-evk.conf

MACHINEOVERRIDES =. "mx93:"
MACHINEOVERRIDES =. "maaxboardbase:maaxboardosm93:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/armv8-2a/tune-cortexa55.inc

MACHINE_FEATURES += " wifi bluetooth"

# Set DDR FIRMWARE
DDR_FIRMWARE_NAME = " \ 
    lpddr4_dmem_1d_v202201.bin \
    lpddr4_dmem_2d_v202201.bin \
    lpddr4_imem_1d_v202201.bin \
    lpddr4_imem_2d_v202201.bin \
"

# Set linux DTB
KERNEL_DEVICETREE = "freescale/${MACHINE}.dtb"

# Set U-boot config
UBOOT_DTB_NAME = "${MACHINE}.dtb"
LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = ""
SPL_BINARY = "spl/u-boot-spl.bin"
UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd]   = "${MACHINE}_defconfig,sdcard"
SERIAL_CONSOLES = "115200;ttyLP0"

# Set ATF platform name
ATF_PLATFORM = "imx93"

# Set imx-mkimage boot target
IMXBOOT_TARGETS = "flash_singleboot_m33"
IMX_BOOT_SOC_TARGET = "iMX9"
IMX_BOOT_SEEK = "32"

# Add additional firmware
SOC_DEFAULT_IMAGE_FSTYPES:append = " wic"

# Add uEnv.txt file to FAT partition
WKS_FILE_DEPENDS:append = " uenv"
IMAGE_BOOT_FILES += "uEnv.txt"
IMAGE_BOOT_FILES += "readme.txt"
IMAGE_BOOT_FILES += "overlays/*;overlays/"

# /etc/hostname
hostname:pn-base-files = "maaxboardosm93"

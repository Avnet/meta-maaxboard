#@TYPE: Machine
#@NAME: Avnet MaaxBoard
#@SOC: i.MX8MQ
#@DESCRIPTION: Machine configuration for Avnet MaaxBoard 2G, DDR4, SDCard
#@MAINTAINER: Wenxue Guo <wenxue.guo@avnet.com>
#@REFERENCE: meta-freescale/conf/machine/imx8mq-evk.conf

MACHINEOVERRIDES =. "mx8:mx8m:mx8mq:"
MACHINEOVERRIDES =. "maaxboardbase:maaxboard:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/armv8a/tune-cortexa53.inc

MACHINE_FEATURES += " wifi bluetooth bcm43455"
MACHINE_SOCARCH_FILTER:append:mx8mq = " virtual/libopenvg virtual/libgles1 virtual/libgles2 virtual/egl virtual/mesa virtual/libgl virtual/libg2d"

KERNEL_DEVICETREE = "freescale/maaxboard.dtb"

UBOOT_SUFFIX = "bin"
UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "maaxboard_defconfig,sdcard"

# Used by poky/meta/recipes-bsp/u-boot/u-boot.inc
SPL_BINARY = "spl/u-boot-spl.bin"

# Set DDR FIRMWARE, Refer to: 
# meta-imx/meta-bsp/recipes-bsp/imx-mkimage/imx-boot_1.0.bb
# meta-imx/meta-bsp/recipes-bsp/firmware-imx/firmware-imx-8_8.18.bb
DDR_FIRMWARE_NAME = " \
    ddr4_imem_1d.bin \
    ddr4_dmem_1d.bin \
    ddr4_imem_2d.bin \
    ddr4_dmem_2d.bin \
"
# Set u-boot DTB 
UBOOT_DTB_NAME = "maaxboard.dtb"

# Set ATF platform name
ATF_PLATFORM = "imx8mq"

# Set imx-mkimage boot target
IMXBOOT_TARGETS = "flash_ddr4_val"
IMX_BOOT_SOC_TARGET = "iMX8M"

# More refer to: meta-freescale/classes/image_types_fsl.bbclass
LOADADDR = ""
UBOOT_MAKE_TARGET = ""
IMX_BOOT_SEEK = "33"
OPTEE_BIN_EXT = "8mq"

# Boot partition size [in KiB] for kernel and other data
# More refer to: meta-freescale/classes/image_types_fsl.bbclass
BOOT_SPACE = "65536"

# Refer to linux-imx/drivers/tty/serial/imx.c
SERIAL_CONSOLES = "115200;ttymxc0"

SOC_DEFAULT_IMAGE_FSTYPES:append = " wic"

# Remove unused QT example program
#CORE_IMAGE_EXTRA_INSTALL:remove += " qtbase-ptest qtbase-examples"

# Add uEnv.txt file to FAT partition
WKS_FILE_DEPENDS:append = " uenv"
IMAGE_BOOT_FILES += "uEnv.txt"
IMAGE_BOOT_FILES += "readme.txt"
IMAGE_BOOT_FILES += "overlays/*;overlays/"

KERNEL_IMAGETYPE = "Image"

# /etc/hostname
hostname:pn-base-files = "maaxboard"
